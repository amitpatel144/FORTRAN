!*****************************************************
!*  Program to demonstrate the BAIRSTOW subroutine   *
!* ------------------------------------------------- *
!* Reference: BASIC Scientific Subroutines, Vol. II  *
!* By F.R. Ruckdeschel, BYTE/McGRAWW-HILL, 1981 [1]. *
!*                                                   *
!*                F90 version by J-P Moreau, Paris.  *
!*                       (www.jpmoreau.fr)           *
!* ------------------------------------------------- *
!* Example: Find two complex roots of polynomial:    *
!*            f(x) = x^5-10x^4+35x^3-50x^2+24x       *
!*                                                   *
!* SAMPLE RUN:                                       *
!*                                                   *
!* Input order of polynomial: 5                      *
!*                                                   *
!* Input the polynomial coefficients:                *
!*                                                   *
!*    A(0) =  0                                      *
!*    A(1) =  24                                     *
!*    A(2) =  -50                                    *
!*    A(3) =  35                                     *
!*    A(4) =  -10                                    *
!*    A(5) =  1                                      *
!*                                                   *
!* Convergence factor: 0.000001                      *
!* Maximum number of iterations: 50                  *
!*                                                   *
!* The roots found are:                              *
!*                                                   *
!*      X1 =  0.100000E+01                           *
!*      Y1 =  0.000000E+00                           *
!*                                                   *
!*      X2 =  0.000000E+00                           *
!*      Y2 =  0.000000E+00                           *
!*                                                   *
!* The number of iterations was:  11                 *
!*                                                   *
!*****************************************************
PROGRAM DEMO_BAIRSTOW

real*8  A(0:10)
integer i,k,m,n
real*8  aa,bb,e,PI,x1,x2,y1,y2

  PI=4.d0*datan(1.d0)
  print *,' '
  write(*,"(' Order of polynomial: ')",advance='no')
  read *, m
  print *,'Input the polynomial coefficients:'
  print *,' '
  do i=0, m
	write(*,50,advance='no') i
	read *, A(i)
  enddo
  print *,' '
  write(*,"(' Convergence factor: ')",advance='no')
  read *, e
  write(*,"(' Maximum number of iterations: ')",advance='no')
  read *, n

  aa=PI; bb=dsqrt(2.d0)

  call Bairstow(m,A,e,n,aa,bb,x1,y1,x2,y2,k)   ! Call Bairstow subroutione

  print *,' '
  print *,'The roots found are:'

  write(*,60) x1
  write(*,70) y1
  write(*,80) x2
  write(*,90) y2
  write(*,100) k

  stop

50  format('      A(',i2,') = ')
60  format(/'      X1 = ',E12.6)
70  format('      Y1 = ',E12.6)
80  format(/'      X2 = ',E12.6)
90  format('      Y2 = ',E12.6)
100 format(/' The number of iterations was: ',i4//)
  
end

!**************************************************
!*       Bairstow complex root subroutine         *
!* ---------------------------------------------- *
!* This routine finds the complex conjugate roots *
!* of a polynomial having real coefficients.      *
!* ---------------------------------------------- *
!* Reference: Computer Methods for Science and    *
!*            Engineering by R.L. Lafara.         *
!* ---------------------------------------------- *
!* INPUTS:                                        *
!*  Polynomial coefficients      : A(0) to A(m)   *
!*  Order of polynomial (>=4)    : m              *
!*  Initial guess                : a and b        *
!*  Convergence factor           : e              *
!*  Maximum number of iterations : n              *
!* OUTPUTS:                                       *
!*  Two conjugate complex roots  : x1,y1  x2,y2   *
!*  Number of iterations         : k              *
!**************************************************
Subroutine Bairstow(m,A,e,n,aa,bb,x1,y1,x2,y2,k)
  ! Labels: 100,200,300,400
  integer i,j,k,m,n
  real*8  A(0:10),B(0:10),C(0:10),D(0:10)
  real*8  aa,bb,e,a1,b1,cc,dd,d2,x1,y1,x2,y2
  !Normalize the A[i] series
  do i=0, m 
    C(i)=A(i)/A(m)
  enddo
  !Take initial estimates for aa and bb
  k=0; B(m)=1.d0
  !Start iteration sequence
100 B(m-1)=C(m-1)-aa
  do j=2,m-1
    B(m-j)=C(m-j)-aa*B(m+1-j)-bb*B(m+2-j)
  end do
  B(0)=C(0)-bb*B(2)
  D(m-1)=-1.d0; D(m-2)=-B(m-1)+aa
  do j=3, m-1
    D(m-j)=-B(m+1-j)-aa*D(m+1-j)-bb*D(m+2-j)
  enddo
  D(0)=-bb*D(2)
  d2=-B(2)-bb*D(3)
  dd=D(1)*d2-D(0)*D(2)
  a1=-B(1)*d2+B(0)*D(2); a1=a1/dd
  b1=-D(1)*B(0)+D(0)*B(1); b1=b1/dd
  aa=aa+a1; bb=bb+b1; k=k+1
  !Test for the number of iterations
  if (k>=n) goto 200
  !Test for convergence
  if (dabs(a1)+dabs(b1)>e*e) goto 100
  !Extract roots from quadratic equation
200 cc=aa*aa-4.d0*bb
  !Test to see if a complex root
  if (cc>0) goto 300
  x1=-aa; x2=x1; y1=dsqrt(-cc); y2=-y1
  goto 400
300 x1=-aa+dsqrt(cc)
  x2=-aa-dsqrt(cc); y1=0; y2=y1
400 x1=x1/2.d0; x2=x2/2.d0; y1=y1/2.d0; y2=y2/2.d0
  return
end


! End of file Bairstow.f90

