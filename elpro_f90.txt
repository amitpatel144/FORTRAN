!********************************************************
!* Eigenvalues and eigenvectors of a real square matrix *
!* by Rutishauser's method and inverse iteration method *
!* ---------------------------------------------------- *
!* Reference:                                           *
!*                                                      *
!*   "ALGEBRE Algorithmes et programmes en Pascal       *
!*    de Jean-Louis Jardrin - Dunod BO-PRE 1988."       *
!*    [BIBLI 10].                                       *
!*                                                      *
!*                    F90 Release By J-P Moreau, Paris. *
!*                           (www.jpmoreau.fr)          *
!* ---------------------------------------------------- *
!* SAMPLE RUN:                                          *
!*                                                      *
!* Input file: elpro.dat                                *
!*                                                      *
!* 5                                                    *
!*   1   2   3  -7    12                                *
!*   2   4   7   3    -1                                *
!*   3   7  10   8     4                                *
!*  -7   3   8  -0.75 -9                                *
!*  12  -1   4  -9    10                                *
!*                                                      *
!* Output to screen:                                    *
!*                                                      *
!*         *** EIGENVALUES AND EIGENVECTORS ***         *
!*              OF A REAL SQUARE MATRIX                 *
!*              BY RUTISHAUSER'S METHOD                 *
!*            AND INVERSE ITERATION METHOD              *
!*                                                      *
!*  Matrix A:                                           *
!*                                                      *
!*   1.0000   2.0000   3.0000  -7.0000  12.000000       *
!*   2.0000   4.0000   7.0000   3.0000  -1.000000       *
!*   3.0000   7.0000  10.0000   8.0000   4.000000       *
!*  -7.0000   3.0000   8.0000  -0.7500  -9.000000       *
!*  12.0000  -1.0000   4.0000  -9.0000  10.000000       *
!*                                                      *
!*                                                      *
!*  Eigenvalue 1:   23.75595                            *
!*                                                      *
!*   Eigenvector:                                       *
!*                                                      *
!*    0.70582  -0.01268   0.13149  -0.52750   1.00000   *
!*                                                      *
!*  Eigenvalue 2:  -10.48655                            *
!*                                                      *
!*   Eigenvector:                                       *
!*                                                      *
!*    0.46717  -0.00795  -0.50783   1.00000   0.26443   *
!*                                                      *
!*  Eigenvalue 3:   0.46335                             *
!*                                                      *
!*   Eigenvector:                                       *
!*                                                      *
!*    0.20881   1.00000  -0.47803  -0.27500  -0.21691   *
!*                                                      *
!*  Eigenvalue 4:   -7.77458                            *
!*                                                      *
!*   Eigenvector:                                       *
!*                                                      *
!*    1.00000  -0.32610   0.20962  -0.14769  -0.81542   *
!*                                                      *
!*  Eigenvalue 5:   0.46335                             *
!*                                                      *
!*   Eigenvector:                                       *
!*                                                      *
!*    0.20881   1.00000  -0.47803  -0.27500  -0.21691   *
!*                                                      * 
!********************************************************
PROGRAM Elpro

integer, parameter :: NMAX = 20

    real*8 d1,d2,eps
    real*8 A(NMAX,NMAX), VX(NMAX,NMAX), R(NMAX)

    print *,' '
    print *,'    *** EIGENVALUES AND EIGENVECTORS ***'
    print *,'          OF A REAL SQUARE MATRIX'
    print *,'          BY RUTISHAUSER''S METHOD'
    print *,'        AND INVERSE ITERATION METHOD'
	print *,' '

!   read data from input text file
    call Data(n, A, eps, d1, d2, m)

    call EPMRI(eps,d1,d2,m,n,A,it,R,VX)

    if (it==-1) then
      print *,'  No convergence!'
	  print *,' '
    else
	  do j=1, n
	    write(*,10) j, R(j)
	    print *,' Eigenvector:'
	    write(*,20)  (VX(i,j),i=1, n)
        print *,' '
		print *,' '
      end do
    end if
    print *,' '  
    stop

10  format(' Eigenvalue ',I1,': ',F10.5)
20  format(5(F10.5)) 

END


  Subroutine Data(n, A, eps, d1, d2, m)
  integer, parameter :: NMAX = 20
  real*8 A(NMAX,NMAX)
  real*8 eps,d1,d2
    
	open(unit=1,file='elpro.dat')

    read(1,*)  n
	print *,' '
    print *,' Matrix A:'
	print *,' '
    do i=1, n
	  read(1,*) (A(i,j),j=1,n)
	  write(*,10) (A(i,j),j=1,n)
    end do  
    print *,' ';
    close(unit=1)
    eps=1.d-10
    d1=1.d-8
    d2=1.d-8
    m=600
	return
10  format(5F10.4)
  End

  !***************************************************************
  !* Subroutine DECCRM determines the lower triangular matrix and*
  !* the upper triangukar matrix of Crout's decomposition of a   *
  !* given square real matrix, A.                                *
  !* ----------------------------------------------------------- *
  !* INPUTS:                                                     *
  !*         eps: required precision (double)                    *
  !*          n : size of matrix A (integer)                     *
  !*          A : input matrix (n x n)                           *
  !* OUTPUTS:                                                    *
  !*          it: flag, =0 if method does not apply              *
  !*                    =1 if method is ok.                      *
  !*           U: lower triangular matrix.                       *
  !*           V: upper triangular matrix.                       *
  !***************************************************************
  Subroutine DECCRM(eps, n, A, it, U, V)
    integer, parameter :: NMAX = 20
    real*8 A(NMAX,NMAX), U(NMAX,NMAX), V(NMAX,NMAX)
    real*8 eps, s

    if (dabs(A(1,1)) < eps)  then
	  it=0
    else
      do i=1, n
	    U(i,1)=A(i,1)
      end do 
      V(1,1)=1.d0
      do j=2, n
	    V(1,j)=A(1,j)/U(1,1)
      end do
      it=1; k=2
      do while (it.ne.0.and.k<=n)
	    do i=1, n
	      if (i < k) then  
		    U(i,k)=0.d0
		  else
	        s=0.d0
	        do j=1, k-1
			  s = s + U(i,j)*V(j,k)
            end do
	        U(i,k) = A(i,k) - s
		  end if
        end do
	    if (dabs(U(k,k)) < eps) then 
          it=0
		else
	      do j=1, n
	        if (j < k) then 
			  V(k,j)=0.d0
	        else if (j==k) then
			  V(k,j)=1.d0
            else
	          s=0.d0
	          do i=1, k-1
			    s = s + U(k,i)*V(i,j)
              end do
	          V(k,j) = A(k,j)/U(k,k);
			end if
          end do
	      k = k + 1
		end if
      end do !while
    end if
	return
  End

!  void MatPrint(char *title, MAT A, int n) {
!  int i,j;
!   printf("\n %s\n", title);
!for (i=1; i<=n; i++) {
!     for (j=1; j<=n; j++)
!       printf(" %f", A[i][j]);
!	  printf("\n");
!   }
! }

! void VecPrint(char *title, VEC X, int n) {
!   int i;
!   printf("\n %s\n", title);
!   for (i=1; i<=n; i++) printf(" %f", X[i]);
!	printf("\n");
! }

  !*********************************************************
  !* Calculate the eigenvalues of a real square matrix by  *
  !* Rutishauser's Method.                                 *
  !* ----------------------------------------------------- *
  !* INPUTS:                                               *
  !*        eps: absolute precision (double)               *
  !*        dta: relative precision (double)               *
  !*         m : maximum number of iterations (integer)    *
  !*         n : size of matrix A (integer)                *
  !*         A : input real square matrix (n x n)          *
  !* OUTPUTS:                                              *
  !*         it: flag, =-1 if convergence is not obtained  *
  !*                   =1 if convergence is ok.            *
  !*         R : contains in output the n eigenvalues of A *
  !*                                                       *         
  !*********************************************************
  Subroutine VAMR(eps, dta, m, n, A, it, R)
    integer, parameter :: NMAX = 20
    real*8 A(NMAX,NMAX), R(NMAX)
    real*8 eps, dta
    real*8 phi,s,t0
    real*8 U(NMAX,NMAX), V(NMAX,NMAX)

    t0=0.d0
    l=1
    do while (l<=m.and.it.ne.1)
      do i=1, n
	    R(i)=A(i,i)
      end do

      call DECCRM(eps, n, A, it, U, V)

      if (it==0) then
	    do i=1, n
		  A(i,i)=A(i,i) + 1.d0
        end do
	    t0 = t0 + 1.d0
      else
	    do i=1, n
		  do j=1, n
	        s=0.d0
	        do k=1, n
			  s = s + V(i,k) * U(k,j)
            end do
	        A(i,j) = s
		  end do
        end do
	    phi=0.d0
        do i=1, n
	      s= dabs(A(i,i)-R(i))
	      if (s > phi)  phi=s
		end do
	    if (phi < dta) then
	      do i=1, n
		    R(i) = A(i,i) - t0
          end do
		else
	      l = l + 1
	      it=-1
		end if
      end if
    end do !while
	return
  End

  !************************************************************
  !* Procedure IIM calculates a real eigenvalue and the asso- *
  !* ciated eigenvector of a real square matrix the inverse   *
  !* iteration method.                                        *
  !* -------------------------------------------------------- *
  !* INPUTS:                                                  *
  !*         eps : absolute precision (double)                *
  !*         dta : relative precision (double)                *
  !*          m  : maximum number of iterations (integer)     *
  !*          n  : size of matrix A                           *
  !*          A  : input real square matrix (n x n)           *
  !* OUTPUTS:                                                 *
  !*          it : flag, =-1 if convergence is not obtained   *
  !*                     =1 if convergence is ok.             *
  !*        Gamma: starting value for the eigenvalue as input *
  !*               approximation of the eigenvalue with preci-*
  !*               sion dta in output.                        *
  !*          X1 : contains in output the associated eigen-   *
  !*               vector.                                    *
  !*                                                          *
  !************************************************************
  Subroutine IIM(eps, dta, m, n, A, it, gamma, X1)
    integer, parameter :: NMAX = 20
    real*8 A(NMAX,NMAX), X1(NMAX)
    real*8 eps, dta, gamma
    real*8  p0,phi,s,t0
    real*8 W(NMAX), X0(NMAX)
    integer LP(NMAX)

    do i=1, n
	  A(i,i) = A(i,i) - gamma
    end do
    do k=1, n-1
      p0=A(k,k); l0=k
      do i=k+1, n
		if (dabs(A(i,k)) > dabs(p0)) then
	      p0=A(i,k); l0=i
		end if
      end do 
      LP(k)=l0
      if (dabs(p0) < eps) then
	    p0=eps; A(l0,k)=eps
      end if
      if (l0.ne.k) then
	    do j=k, n
	      t0=A(k,j); A(k,j)=A(l0,j); A(l0,j)=t0
		end do
      end if
	  do i=k+1, n
	    A(i,k)=A(i,k)/p0
	    do j=k+1, n
	      A(i,j)=A(i,j)-A(i,k)*A(k,j)
        end do
      end do
	end do !k loop

    if (dabs(A(n,n)) < eps)  A(n,n)=eps
    do i=1, n
	  X0(i)=1.d0/sqrt(1.d0*i)
    end do 
    it=-1; l=1
    do while (it==-1.and.l<=m)
      do i=1, n
	    W(i)=X0(i)
      end do
      do k=1, n-1
        l0=LP(k)
        if (l0.ne.k) then
          t0=W(k); W(k)=W(l0); W(l0)=t0
        end if
        do i=k+1, n
		  W(i)=W(i)-A(i,k)*W(k)
        end do
      end do
      X1(n)=W(n)/A(n,n)
      do i=n-1, 1, -1
        s=0.d0
        do j=i+1, n
		  s = s + A(i,j)*X1(j)
        end do
	    X1(i)=(W(i)-s)/A(i,i)
      end do
      p0=0.d0
      do i=1, n
        if (dabs(X1(i)) > dabs(p0))  p0=X1(i)
      end do
      do i=1, n
        X1(i)=X1(i)/p0
      end do
      phi=0.d0
      do i=1, n
        s=dabs(X1(i)-X0(i))
        if (s > phi)  phi=s
      end do

      if (phi < dta) then
        gamma = gamma + 1.d0/p0
        it=1
      else
        do i=1, n
		  X0(i)=X1(i)
        end do 
        l = l + 1
      end if
	end do !while
	return
  End  !IIM  	  						    


  !******************************************************
  !* INPUTS:                                            *
  !* EPS : precision (Double)                           *
  !* D1  : precision d1 (Double)                        *
  !* D2  : precision d2 (Double)                        *
  !* M   : maximum number of iterations (integer)       *
  !* N   : order of matrix A (integer)                  *
  !* A   : input matrix to study (of MAT type)          *
  !* -------------------------------------------------- *
  !* OUTPUTS:                                           *
  !* IT  : -1 if no convergence is obtained (integer)   *
  !* R   : table of eigenvalues (of VEC type)           *
  !* VX  : table of eigenvectors (of MAT type)          *
  !******************************************************
  Subroutine EPMRI(eps, d1, d2, m, n, A, it, R, VX)
    integer, parameter :: NMAX = 20
    real*8 A(NMAX,NMAX), R(NMAX), VX(NMAX,NMAX)
    real*8 X(NMAX), A1(NMAX,NMAX)

    do i=1, n
      do j=1, n
        A1(i,j) = A(i,j)
      end do
    end do

    call VAMR(eps,d2,m,n,A1,it,R)

! restore A1 after VAMR
    do i=1, n
      do j=1, n
        A1(i,j) = A(i,j)
      end do
	end do   

    j=1
    do while (it==1.and.j<=n)

    call IIM(eps,d1,m,n,A1,it,R(j),X)
  
! restore A1 after IIM
      do i=1, n
        do k=1, n
          A1(i,k) = A(i,k)
        end do
        VX(i,j) = X(i)
      end do
      j = j + 1     
    end do !while
	
	return
  End

! End of file elpro.f90
