!*****************************************************************************
!*     Step by step solution of system [M] X" + [C] X' + [K] X = F(t)        *
!*                    by the "Wilson-Theta" Method                           *
!* ------------------------------------------------------------------------- *
!* SAMPLE RUN:                                                               *
!*                                                                           *
!* Let us consider the 3 d.o.f. Mass-Spring system with viscous damping:     *
!*                                                                           *
!*                      --> x1          --> x2    k     --> x3               *
!*          \|    k     *----*    2k    *----*--/\/\/\--*----*               *
!*          \|--/\/\/\--| 2m |--/\/\/\--| m  |  ---*    | 3m |               *
!*          \|          *----*          *----*---| |----*----*               *
!*                      --> F1(t)               ---*                         *
!*                                                c                          *
!*                      | 2  0  0 |                                          *
!* The Mass Matrix M is | 0  1  0 |  assuming m=1.                           *
!*                      | 0  0  3 |                                          *
!*                                                                           *
!*                           |  3 -2  0 |                                    *
!* The Stiffness Matrix K is | -2  3 -1 |  assuming k=1.                     *
!*                           |  0 -1  1 |                                    *
!*                                                                           *
!*                       | 0  0  0   |                                       *
!* The Damping Matrix is | 0  0  0   | assuming c=0.5.                       *
!*                       | 0  0  0.5 |                                       *
!*                                                                           *
!* The resonance fresuencies (neglecting c) are:                             *
!*             F1 = w1/2pi = 0.05161 sqr(k/m)                                *
!*             F2 = w2/2pi = 0.1431 sqr(k/m)                                 *
!*             F3 = w3/2pi = 0.3151 sqr(k/m)                                 *
!* with the corresponding modal vectors:                                     *
!*             |   1   |         |   1    |         |    1    |              *
!*      phi1 = | 1.395 |  phi2 = | 0.6914 |  phi3 = | -2.420  |              *
!*             | 2.038 |         |-0.4849 |         |  0.2249 |              *
!*                                                                           *
!* (See program ndof03.f90).                                                 *
!*                                                                           *
!*                                                                           *
!* How many degrees of freedom (d.o.f.): 3                                   *
!*                                                                           *
!* Mass Matrix:                                                              *
!*  M(1,1) = 2                                                               *
!*  M(1,2) = 0                                                               *
!*  M(1,3) = 0                                                               *
!*  M(2,2) = 1                                                               *
!*  M(2,3) = 0                                                               *
!*  M(3,3) = 3                                                               *
!*                                                                           *
!* Damping Matrix:                                                           *
!*  C(1,1) = 0                                                               *
!*  C(1,2) = 0                                                               *
!*  C(1,3) = 0                                                               *
!*  C(2,2) = 0                                                               *
!*  C(2,3) = 0                                                               *
!*  C(3,3) = 0.5                                                             *
!*                                                                           *
!* Stiffness Matrix:                                                         *
!*  K(1,1) = 3                                                               *
!*  K(1,2) = -2                                                              *
!*  K(1,3) = 0                                                               *
!*  K(2,2) = 3                                                               *
!*  K(2,3) = -1                                                              *
!*  K(3,3) = 1                                                               *
!*                                                                           *
!* Starting time (sec.) = 0                                                  *
!* Ending time......... = 60                                                 *
!* Time increment...... = 0.1                                                *
!*                                                                           *
!* Starting motion X(1) = 0                                                  *
!* Starting motion X(2) = 0                                                  *
!* Starting motion X(3) = 0                                                  *
!*                                                                           *
!* Starting speed V(1) = 0                                                   *
!* Starting speed V(2) = 0                                                   *
!* Starting speed V(3) = 0                                                   *
!*                                                                           *
!* Starting acceleration G(1) = 0                                            *
!* Starting acceleration G(2) = 0                                            *
!* Starting acceleration G(3) = 0                                            *
!*                                                                           *
!* Theta = 1.4                                                               *
!*                                                                           *
!* Number of force componant: 1                                              *
!*                                                                           *
!* Force (maximum) = 1000                                                    *
!* Force frequency = 0.315                                                   *
!*                                                                           *
!* Output file ndof04.txt contains:                                          *
!*                                                                           *
!*   Time=  0.000000000000000E+000 Force=  0.000000000000000E+000            *
!*   Node  Displacement  Speed  Acceleration                                 *
!*   ---------------------------------------                                 *
!* 1      0.000000      0.000000      0.000000                               *
!* 2      0.000000      0.000000      0.000000                               *
!* 3      0.000000      0.000000      0.000000                               *
!*                                                                           *
!*  Time=  0.100000000000000      Force=   196.630694615420                  *
!* 1      0.163063      4.891900     97.838009                               *
!* 2      0.001055      0.031650      0.633007                               *
!* 3      0.000001      0.000034      0.000681                               *
!*  Time=  0.200000000000000      Force=   385.583992277397                  *
!* 1      1.342110     20.695684    218.237660                               *
!* 2      0.012156      0.238073      3.495455                               *
!* 3      0.000017      0.000368      0.006008                               *
!*  Time=  0.300000000000000      Force=   559.482258102167                  *
!* 1      4.697087     48.346076    334.770183                               *
!* 2      0.065333      0.944405     10.631169                               *
!* 3      0.000120      0.002070      0.028018                               *
!*  Time=  0.400000000000000      Force=   711.535677209285                  *
!* 1     11.373234     86.853736    435.383013                               *
!* 2      0.235185      2.675173     23.984201                               *
!* 3      0.000575      0.008105      0.092686                               *
!*  Time=  0.500000000000000      Force=   835.807361368270                  *
!* 1     22.364561    134.263211    512.806499                               *
!* 2      0.657746      6.127269     45.057710                               *
!* 3      0.002104      0.025013      0.245478                               *
!*  Time=  0.600000000000000      Force=   927.445153334661                  *
!* 1     38.436915    188.003858    562.006436                               *
!* 2      1.545163     12.115093     74.698783                               *
!* 3      0.006349      0.065060      0.555465                               *
!* ---    --------      --------      --------                               *
!*  Time=   59.5000000000006      Force=  -998.889874962023                  *     
!* 1   1311.501557  -4027.911278  -5149.756316                               *
!* 2  -2554.792157   9594.998127  10332.611203                               *
!* 3    184.399735   -884.959493   -754.711909                               *
!*  Time=   59.6000000000006      Force=  -988.651744737743                  * 
!* 1    885.667274  -4461.718120  -3526.380524                               *
!* 2  -1550.158169  10432.392829   6415.282844                               *
!* 3     92.748603   -941.879364   -383.685509                               *
!*  Time=   59.7000000000006      Force=  -939.811951085932                  *
!* 1    424.805036  -4726.111878  -1761.494639                               *
!* 2   -481.806426  10865.002501   2236.910583                               *
!* 3     -2.712865   -960.901048      3.251826                               *
!*  Time=   59.8000000000006      Force=  -854.277431698705                  *
!* 1    -53.549682  -4810.343067     76.870845                               *
!* 2    608.748956  10874.810930  -2040.741986                               *
!* 3    -98.140459   -941.188318    391.002771                               *
!*  Time=   59.9000000000006      Force=  -735.387860780246                  *
!* 1   -531.131704  -4710.618045   1917.629602                               *
!* 2   1679.008275  10460.194795  -6251.580732                               *
!* 3   -189.681955   -883.418370    764.396191                               *
!*  Time=   60.0000000000006      Force=  -587.785252291543                  *
!* 1   -989.652417  -4430.266778   3689.395729                               *
!* 2   2687.136442   9636.034476 -10231.625630                               *
!* 3   -273.627843   -789.759696   1108.777301                               *
!*                                                                           *
!*  Maximum Displacement (node #1) =    2432.18400402504                     *
!*                                                                           *
!* ------------------------------------------------------------------------- *
!* REFERENCE:  "Mécanique des vibrations linéaires By M. Lalanne,            *
!*              P. Berthier, J. Der Hagopian, Masson, Paris 1980".           *
!*              [BIBLI 16].                                                  *
!*                                                                           *
!*                                        F90 Release By J-P Moreau, Paris.  *
!*                                               (www.jpmoreau.fr)           *
!*****************************************************************************
Program ndof04

implicit real*8 a-h,o-z

integer, parameter :: NMAX = 16

REAL*8    M(NMAX, NMAX), K(NMAX, NMAX), K1(NMAX, NMAX)
DIMENSION B1(NMAX, 2)
DIMENSION A(NMAX, NMAX), C(NMAX, NMAX)
DIMENSION X0(NMAX), V0(NMAX), G0(NMAX), F0(NMAX), F1(NMAX), F2(NMAX)
DIMENSION X1(NMAX), V1(NMAX),G1(NMAX), X2(NMAX)

PI = 4.d0 * DATAN(1.d0)

!open output text file
OPEN(UNIT=1,FILE='ndof4.txt',STATUS='UNKNOWN')

! Begin Data section
PRINT *,' '
WRITE(*,10,advance='no'); READ *, N
PRINT *,' '
PRINT *,'Mass Matrix:'
DO I = 1, N
  DO J = I, N
    WRITE(*,20,advance='no') I, J; READ *, M(I,J)
  END DO
END DO

PRINT *,' '
PRINT *,'Damping Matrix'
DO I = 1, N
  DO J = I, N
    WRITE(*,30,advance='no') I, J; READ *, C(I,J)
  END DO
END DO

PRINT *,' '
PRINT *,'Stiffness Matrix:'
DO I = 1, N
  DO J = I, N
    WRITE(*,40,advance='no') I, J; READ *, K(I,J)
  END DO
END DO

!Complete Matrices by symmetry
DO I = 1, N
  DO J = I, N
    K(J, I) = K(I, J)
    C(J, I) = C(I, J)
    M(J, I) = M(I, J)
  END DO
END DO

PRINT *,' '
WRITE(*,50,advance='no'); READ *, T0
WRITE(*,60,advance='no'); READ *, T3
WRITE(*,70,advance='no'); READ *, D

PRINT *,' '
DO I = 1, N
  WRITE(*,80,advance='no') I; READ *, X0(I)
END DO
PRINT *,' '
DO I = 1, N
  WRITE(*,90,advance='no') I; READ *, V0(I)
END DO
PRINT *,' '
DO I = 1, N
  WRITE(*,100,advance='no') I; READ *, G0(I)
END DO
PRINT *,' '
WRITE(*,110,advance='no'); READ *, T2
PRINT *,' '
WRITE(*,120,advance='no'); READ *, N1
PRINT *,' '
WRITE(*,130,advance='no'); READ *, Fmax
WRITE(*,140,advance='no'); READ *, freq
F = Fmax * DSIN(2.d0 * PI * freq * T0)
! end data section

WRITE(1,*) ' '
WRITE(1,*) ' Time=', T0, ' Force=', F
WRITE(1,*) ' Node  Displacement  Speed  Acceleration'
WRITE(1,*) ' ---------------------------------------'
DO I = 1, N
  WRITE(1,200) I, X0(I), V0(I), G0(I)
END DO
WRITE(1,*) ' '
F0 = 0.d0
F0(N1) = F
A9 = T2 * D
A0 = 6.d0 / A9**2
A1 = 3.d0 / A9
A2 = 2.d0 * A1
A3 = A9 / 2.d0
A4 = A0 / T2
A5 = -A2 / T2
A6 = 1.d0 - 3.d0 / T2
A7 = D / 2.d0
A8 = D**2 / 6.d0

!Construct K+A0*M+A1*C
!---------------------
500 F1 = 0.d0
T1 = T0 + D
F = Fmax * DSIN(2.d0 * PI * freq * T1)
WRITE(1,*) ' Time=', T1, ' Force=', F
F1(N1) = F
F2 = 0.d0
DO I = 1, N
  F2(I) = F0(I) + T2 * (F1(I) - F0(I))
  DO J = 1, N
    K1(I, J) = K(I, J) + A0 * M(I, J) + A1 * C(I, J)
    F2(I) = F2(I) + M(I, J) * (A0 * X0(J) + A2 * V0(J) + 2.d0 * G0(J))
    F2(I) = F2(I) + C(I, J) * (A1 * X0(J) + 2.d0 * V0(J) + A3 * G0(J))
  END DO
END DO

!K1=INV(K1)
CALL MATINV(N,0,K1,B1,DET)

!X2=K1 MPY F2
DO I = 1, N
  SUM = 0.d0
  DO J = 1, N
    SUM = SUM + K1(I, J) * F2(J)
  END DO
  X2(I) = SUM
END DO

DO I = 1, N
  G1(I) = A4 * (X2(I) - X0(I)) + A5 * V0(I) + A6 * G0(I)
  V1(I) = V0(I) + A7 * (G1(I) + G0(I))
  X1(I) = X0(I) + D * V0(I) + A8 * (G1(I) + 2.d0 * G0(I))
  IF (DABS(X1(1)) > XMAX)  XMAX = X1(1)
END DO

DO I = 1, N
  WRITE(1,200) I, X1(I), V1(I), G1(I)
END DO
DO I = 1, N
  X0(I) = X1(I); V0(I) = V1(I); G0(I) = G1(I)
END DO
T0 = T1
IF (T1 < T3) GOTO 500  !next time step

PRINT *,' '
PRINT *,' Results in file ndof4.txt...'
PRINT *,' '

WRITE(1,*) ' '
WRITE(1,*) ' Maximum Displacement (node #1) = ', XMAX
WRITE(1,*) ' '

CLOSE(UNIT=1)

10 FORMAT(' How many degrees of freedom (d.o.f.): ')
20 FORMAT('  M(',I2,',',I2,') = ')
30 FORMAT('  C(',I2,',',I2,') = ')
40 FORMAT('  K(',I2,',',I2,') = ')
50 FORMAT(' Starting time (sec.) = ')
60 FORMAT(' Ending time......... = ')
70 FORMAT(' Time increment...... = ')
80 FORMAT(' Starting motion X(',I2,') = ')
90 FORMAT(' Starting speed V(',I2,') = ')
100 FORMAT(' Starting acceleration G(',I2,') = ')
110 FORMAT(' Theta = ')
120 FORMAT(' Number of force componant: ')
130 FORMAT(' Force (maximum) = ')
140 FORMAT(' Force frequency = ')

200 FORMAT(I2,3F14.6)

END !of main program


!*******************************************
!*  SOLVING A LINEAR MATRIX SYSTEM AX = B  *
!*  with Gauss-Jordan method using full    *
!*  pivoting at each step. During the pro- *
!* cess, original A and B matrices are     *
!* destroyed to spare storage location.    *
!* --------------------------------------- *
!* INPUTS:    A   MATRIX N*N               *
!*            B   MATRIX N*M               *
!* --------------------------------------- *
!* OUTPUS:    A   INVERSE OF A N*N         *
!*            DET  DETERMINANT OF A        *
!*            B   SOLUTION MATRIX N*M      *
!* --------------------------------------- *
!* NOTA - If M=0 inversion of A matrix     *
!*        only.                            *
!*******************************************
      SUBROUTINE MATINV(N,M,AA,BB,DET)
      integer, parameter :: NMAX = 16
      REAL*8,PARAMETER :: EPSMACH = 2.D-16                                    
      REAL*8 AA(NMAX,*),BB(NMAX,*)                                                   
      INTEGER PC(N),PL(N)
	  REAL*8 CS(N)                                                  
      REAL*8 PV,PAV,DET,TT    
	         
!Initializations :                       
      DET=1.
      DO I=1,N                                                                                                                   
        PC(I)=0
	    PL(I)=0
	    CS(I)=0.
      END DO
!main loop                                                                   
      DO K=1,N                                                                  
!Searching greatest pivot :                                               
        PV=AA(K,K)                                                              
        IK=K                                                                    
        JK=K                                                                    
        PAV=ABS(PV)                                                            
        DO I=K,N                                                                
          DO J=K,N                                                              
            IF (ABS(AA(I,J)).GT.PAV) THEN                                      
              PV=AA(I,J)                                                        
              PAV=ABS(PV)                                                      
              IK=I                                                              
              JK=J                                                              
            ENDIF                                                               
          ENDDO                                                                 
        ENDDO                                                                   
                                                                               
!Search terminated, the pivot is in location I=IK, J=JK.
!Memorizing pivot location: :                                        
        PC(K)=JK                                                                
        PL(K)=IK                                                                
                                                                               
!Determinant DET is actualised
!If DET=0, ERROR MESSAGE and STOP
!Machine dependant EPSMACH equals here 1.DE-20                                        
                                                                               
        IF (IK.NE.K) DET=-DET                                                   
        IF (JK.NE.K) DET=-DET                                                   
        DET=DET*PV                                                              
        IF (ABS(DET).LT.EPSMACH) THEN                                          
!Error message and Stop                                                     
          PRINT 10                                                              
          STOP                                                                  
        ENDIF                                                                   
                                                                               
!POSITIONNING PIVOT IN K,K:                                              
        IF(IK.NE.K) THEN                                                        
          DO I=1,N                                                              
!EXCHANGE LINES IK and K:                                            
            TT=AA(IK,I)                                                         
            AA(IK,I)=AA(K,I)                                                    
            AA(K,I)=TT                                                          
          ENDDO                                                                 
        ENDIF                                                                   
      IF (M.NE.0) THEN                                                          
        DO I=1,M                                                               
          TT=BB(IK,I)                                                           
          BB(IK,I)=BB(K,I)                                                      
          BB(K,I)=TT                                                            
        ENDDO                                                                   
      ENDIF                                                                     
!Pivot is at correct line                                                
        IF(JK.NE.K) THEN                                                        
          DO I=1,N                                                              
!Exchange columns JK and K of matrix AA                                         
            TT=AA(I,JK)                                                         
            AA(I,JK)=AA(I,K)                                                    
            AA(I,K)=TT                                                          
          ENDDO                                                                 
        ENDIF                                                                   
!Pivot is at correct column and located in K,K                                              
                                                                               
!Store column K in vector CS                             
!then set column K to zero                                             
        DO I=1,N                                                                
          CS(I)=AA(I,K)                                                         
          AA(I,K)=0.D0                                                          
        ENDDO                                                                   
!                                                                               
        CS(K)=0.                                                                
        AA(K,K)=1.                                                              
!Modify line K :                                            
        IF(ABS(PV).LT.EPSMACH) THEN                                            
          WRITE(*,*) '  PIVOT TOO SMALL - STOP'                               
          STOP                                                                  
        ENDIF                                                                   
        DO I=1,N                                                                
          AA(K,I)=AA(K,I)/PV                                                    
        ENDDO                                                                   
        IF (M.NE.0) THEN                                                        
          DO I=1,M                                                             
            BB(K,I)=BB(K,I)/PV                                                  
          ENDDO                                                                 
        ENDIF                                                                   
!Modify other lines of matrix AA:                                        
        DO J=1,N                                                                
          IF (J.EQ.K) CONTINUE                                                  
          DO I=1,N                                                              
!Modify line J of matrix AA :                                            
            AA(J,I)=AA(J,I)-CS(J)*AA(K,I)                                       
          ENDDO                                                                 
          IF (M.NE.0) THEN                                                      
            DO I=1,M                                                          
              BB(J,I)=BB(J,I)-CS(J)*BB(K,I)                                     
            ENDDO                                                               
          ENDIF                                                                 
        ENDDO                                                                   
!Line K is ready.                                                
      ENDDO                                                                     
!End of K loop                                                              
                                                                               
!The matrix AA is inverted - Rearrange AA                         
                                                                               
!Exchange lines                                                            
      DO I=N,1,-1                                                               
        IK=PC(I)                                                                
        IF (IK.EQ.I) CONTINUE                                                   
!EXCHANGE LINES I AND PC(I) OF AA:                                         
        DO J=1,N                                                                
          TT=AA(I,J)                                                            
          AA(I,J)=AA(IK,J)                                                      
          AA(IK,J)=TT                                                           
        ENDDO                                                                   
        IF (M.NE.0) THEN                                                        
          DO J=1,M                                                             
            TT=BB(I,J)                                                          
            BB(I,J)=BB(IK,J)                                                    
            BB(IK,J)=TT                                                         
          ENDDO                                                                 
        ENDIF                                                                   
!NO MORE EXCHANGE NEEDED                                                      
!GO TO NEXT LINE                                                  
      ENDDO                                                                     
                                                                               
!EXCHANGE COLUMNS                                                          
                                                                               
      DO J=N,1,-1                                                               
        JK=PL(J)                                                                
        IF (JK.EQ.J) CONTINUE                                                   
!EXCHANGE COLUMNS J AND PL(J) OF AA :                                       
        DO I=1,N                                                                
          TT=AA(I,J)                                                            
          AA(I,J)=AA(I,JK)                                                      
          AA(I,JK)=TT                                                           
        ENDDO                                                                   
!NO MORE EXCHANGE NEEDED                                                      
!GO TO NEXT COLUMN   
      ENDDO                                                                     
!REARRANGEMENT TERMINATED.                                                        
      RETURN                                                                    
   10 FORMAT(//'  DETERMINANT EQUALS ZERO, NO SOLUTION!'/)                    
      END                 

!end of file ndof4.f90
